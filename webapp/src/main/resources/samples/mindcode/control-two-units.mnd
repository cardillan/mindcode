// Binds two different units, stores them in variables and controls
// both them simultaneously by moving them around in a pattern

// Units is a system library providing functions
// for easy acquisition of units
require units;

// We'll write messages onto this block
// It is used by the units system library to output messages as well
param MESSAGES = message1;

// Finds a free unit of given type, not returning until such a unit is found
// Sets the flags of these units to a nonzero value
// Stores the acquired units in variables
poly = waitForFreeUnit(MESSAGES, @poly, 1);
mega = waitForFreeUnit(MESSAGES, @mega, 1);

// This is a global variable
ANGLE = 0;

while true do
    controlUnit(poly);
    controlUnit(mega);
end;

def controlUnit(unit)
    // Binds the unit stored in a variable
    ubind(unit);
    move(@thisx + 10 * sin(ANGLE), @thisy + 10 * cos(ANGLE));
    print($"Currently bound unit is ${@unit}.");
    printflush(MESSAGES);
    wait(0.8);
    ANGLE += 45;
end;
