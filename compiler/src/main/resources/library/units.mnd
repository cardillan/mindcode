// Mindcode system library
// Units
//
//* To use the Units library, use the `require units;`

/**
 * Finds and binds a free unit of given type. When such a unit is found, it is flagged by the given initial flag.
 * If no free unit of given type can be found (either because none exists, or because all existing units are occupied),
 * returns immediately.
 *
 * Inputs and outputs:
 *
 * - `unit_type`: type of the unit: `@flare`, `@mono`, `@poly` etc. Can be a variable.
 * - `initial_flag`: initial flag to set to he freshly bound unit.
 * - returns the freshly bound unit, or `null` if no free unit of given type exists
 *
 * The function doesn't use units that are controlled by a player or a different processor.
 */
def findFreeUnit(_unit_type, _initial_flag)
    ubind(_unit_type);
    // No unit of this type exists
    if @unit == null then return null; end;

    _first_unit = @unit;
    do
        if @unit.@controlled == 0 then
            flag(_initial_flag);        // Mark unit as active
            return @unit;
        elsif _first_unit.@dead == 1 then
            // _first_unit got killed. It won't be served by ubind anymore
            // Replace with the current one
            _first_unit = @unit;
        end;

        // Try the next one. If it is the same as the first one, we've seen them all
        ubind(_unit_type);
    loop while _first_unit != @unit;

    return null;
end;

/**
 * Searches for and binds a free unit of given type closest to the coordinates on the map given.
 * If no free unit of given type can be found (either because none exists, or because all existing units
 * are occupied), returns immediately.
 *
 * Inputs and outputs:
 *
 * - `x`, `y`: position of the map to compute unit distance relative to
 * - `unit_type`: type of the unit: `@flare`, `@mono`, `@poly` etc. Can be a variable.
 * - `initial_flag`: initial flag to set to he freshly bound unit.
 * - returns the freshly bound unit closest to given coordinates, or `null` if no free unit of given type exists
 *
 * The function doesn't use units that are controlled by a player or a different processor.
*/
def findClosestUnit(_x, _y, _unit_type, _initial_flag)
    while true do
        ubind(_unit_type);
        // No unit of this type exists
        if @unit == null then return null; end;

        _first_unit = @unit;
        _best_unit = null;
        _best_distance = 1e10;
        do
            if @unit.@controlled == 0 then
                _distance = len(@unit.@x - _x, @unit.@y - _y);
                if _distance < _best_distance then
                    _best_distance = _distance;
                    _best_unit = @unit;
                end;
            elsif _first_unit.@dead == 1 then
                // _first_unit got killed. It won't be served by ubind anymore
                // Replace with the current one
                _first_unit = @unit;
            end;

            // Try the next one. If it is the same as the first one, we've seen them all
            ubind(_unit_type);
        loop while _first_unit != @unit;

        // No free unit at all
        if _best_unit == null then
            return null;
        end;

        // If the best unit is still not controlled, flag it and return
        if _best_unit.@controlled == 0 then
            ubind(_best_unit);
            flag(_initial_flag);
            return _best_unit;
        end;
    end;
end;

/**
 * Finds and binds a free unit of given type. When such a unit is found, it is flagged by the given initial flag.
 * The function doesn't return until a free unit of the given type can be found,
 *
 * Inputs and outputs:
 *
 * - `unit_type`: type of the unit: `@flare`, `@mono`, `@poly` etc. Can be a variable.
 * - `initial_flag`: initial flag to set to he freshly bound unit.
 * - returns the freshly bound unit
 *
 * The function doesn't use units that are controlled by a player or a different processor.
 *
 * The status of the search is output to `SYS_MESSAGE`. Either set the message to an existing memory block to receive
 * the updates, or set `SYS_MESSAGE` to `null` to disable status updates (`const SYS_MESSAGE = null;` is also supported).
 */
def waitForFreeUnit(_unit_type, _initial_flag)
    // Keep looking for unit until one is found
    while true do
        ubind(_unit_type);
        if @unit == null then
            if SYS_MESSAGE then
                print($"No unit of type $_unit_type found.");
                printflush(SYS_MESSAGE);
             end;
        elsif @unit.@controlled != 0 then
            if SYS_MESSAGE then
                print($"Looking for a free $_unit_type...");
                printflush(SYS_MESSAGE);
             end;
        else
            flag(_initial_flag);        // Mark unit as active
            return @unit;
        end;
    end;
end;

