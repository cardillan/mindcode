// Mindcode system library for Mindustry Logic version 8
// Printing - numeric output
//
//* To use the Printing library, use the `require printing;` statement. Some of the Printing library functions use
//* Mindustry Logic 8 instructions and therefore require the `#set target = 8;` statement.

module printing;

require math;

/**
 * Clears the processor's text buffer without outputting the contents into any message block. Equivalent to
 * `printflush(null);`.
 */
inline void printflush()
    printflush(null);
end;

/**
 * Prints all arguments passed to it, each on a new line.
 */
inline void printLines(lines...)
    for var line in lines do
        println(line);
    end;
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Formats the number passed in as a parameter into the text buffer, using commas as thousand separators.
 * The fractional part of the number to be printed is ignored.
 *
 * To use the function, a placeholder must be inserted into the text buffer prior to the call of this function.
 * The placeholder must be `{2}` or higher, placeholders `{0}` and `{1}` are reserved. The number will be rendered
 * at the place of the lowest formatting placeholder.
 *
 * Nulls are printed as 0.
 *
 * > [!TIP]
 * > While the function is optimized for performance, formatting numbers is many times slower than just printing
 * > them using the `print()` function.
 */
void formatNumber(number)
    var n = floor(abs(number));
    if number < 0 then
        format("-{2}");     // Prepend the minus sign
    end;
    while n > 999 do
        var mod = n % 1000;
        // Insert placeholder for the next group, thousands separator, leading zeroes (if any) and a placeholder for this group.
        format(mod < 10 ? "{2},00{1}" : mod < 100 ? "{2},0{1}" : "{2},{1}");
        format(mod);
        n \= 1000;
    end;

    // Put the rest of the number into the remaining placeholder
    format(n);
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Prints the number passed in as a parameter into the text buffer, using commas as thousand separators.
 * The fractional part of the number to be printed is ignored.
 *
 * To use the function, the text buffer must not contain placeholders `{0}`, `{1}` or `{2}`.
 * The number will be printed at the end of the print buffer.
 *
 * Nulls are printed as 0.
 *
 * > [!TIP]
 * > While the function is optimized for performance, formatting numbers is many times slower than just printing
 * > them using the `print()` function.
 *
 * See also [`formatNumber`](#formatnumber)
 */
void printNumber(number)
    print("{2}");
    formatNumber(number);
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Formats the number passed in as a parameter into the text buffer in binary base, outputting at least the given
 * number of binary digits. The fractional part of the number to be printed is ignored.
 *
 * To use the function, a placeholder must be inserted into the text buffer prior to the call of this function.
 * The placeholder must be `{2}` or higher, placeholders `{0}` and `{1}` are reserved. The number will be rendered
 * at the place of the lowest formatting placeholder.
 *
 * Nulls are printed as zero values.
 *
 * **Inputs and outputs:**
 *
 * - `number`: number to be formatted
 * - `digits`: minimal number of digits to output (padded on the left by zeroes)
 */
void formatBinaryNumber(number, digits)
    var n = floor(number);
    do
        format("{2}{1}", n & 1);
        n >>= 1;
        --digits;
    while n > 0;

    while digits > 0 do
        format("{2}0");
        digits--;
    end;

    // Binary prefix
    format("0b");
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Prints the number passed in as a parameter into the text buffer in binary base, outputting at least the given
 * number of binary digits. The fractional part of the number to be printed is ignored.
 *
 * To use the function, the text buffer must not contain placeholders `{0}`, `{1}` or `{2}`.
 * The number will be printed at the end of the print buffer.
 *
 * Nulls are printed as zero values.
 *
 * See also [`formatBinaryNumber`](#formatbinarynumber)
 *
 * **Inputs and outputs:**
 *
 * - `number`: number to be formatted
 * - `digits`: minimal number of digits to output (padded on the left by zeroes)
 */
void printBinaryNumber(number, digits)
    print("{2}");
    formatBinaryNumber(number, digits);
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Formats the number passed in as a parameter into the text buffer in hexadecimal base, outputting at least the given
 * number of hexadecimal digits. The fractional part of the number to be printed is ignored.
 *
 * To use the function, a placeholder must be inserted into the text buffer prior to the call of this function.
 * The placeholder must be `{2}` or higher, placeholders `{0}` and `{1}` are reserved. The number will be rendered
 * at the place of the lowest formatting placeholder.
 *
 * Nulls are printed as zero values.
 *
 * **Inputs and outputs:**
 *
 * - `number`: number to be formatted
 * - `digits`: minimal number of digits to output (padded on the left by zeroes)
 */
void formatHexNumber(number, digits)
    var n = floor(number);
    do
        var digit = n % 16;
        case digit
            when 0 .. 9 then format("{2}{1}", digit);
            when 10 then format("{2}A");
            when 11 then format("{2}B");
            when 12 then format("{2}C");
            when 13 then format("{2}D");
            when 14 then format("{2}E");
            when 15 then format("{2}F");
        end;
        n >>= 4;
        --digits;
    while n > 0;

    while digits > 0 do
        format("{2}0");
        digits--;
    end;

    // Hexadecimal prefix
    format("0x");
end;

/**
 * **Note:** Function requires Mindustry Logic version 8 or later.
 *
 * Prints the number passed in as a parameter into the text buffer in hexadecimal base, outputting at least the given
 * number of hexadecimal digits. The fractional part of the number to be printed is ignored.
 *
 * To use the function, the text buffer must not contain placeholders `{0}`, `{1}` or `{2}`.
 * The number will be printed at the end of the print buffer.
 *
 * Nulls are printed as zero values.
 *
 * See also [`formatBinaryNumber`](#formatbinarynumber)
 *
 * **Inputs and outputs:**
 *
 * - `number`: number to be formatted
 * - `digits`: minimal number of digits to output (padded on the left by zeroes)
 */
void printHexNumber(number, digits)
    print("{2}");
    formatHexNumber(number, digits);
end;


/**
 * Prints the value into the text buffer without rounding to the nearest integer value.
 * The function is primarily useful for debugging purposes to determine the actual value of variables.
 * This variant of the function is optimized to produce faster, larger code.
 *
 * **Note:** When printing the exact representation of numbers that are close to integer value, the Mindustry Logic
 * `print` instruction cannot be used, as it would print the rounded value. In such a case a custom printing routine
 * is used, which can take around 50 steps to output the entire number.
 *
 * > [!TIP]
 * > The `print` and `format` instructions applied to numerical values round the value to the nearest integer
 * > when they differ from the integer value by less than `1e-6`. This makes it impossible to directly print
 * > (with sufficient precision) numerical values close to integer values in general, and values close to zero
 * > in particular. For example, `print(10 ** -50)` prints `0`. On the other hand, `printExactFast(10 ** -50)`
 * > outputs `1E-50`.
 *
 * > [!IMPORTANT]
 * > The mathematical operations used by `printExact` to output the value may introduce small additional numerical
 * > errors to the output value; for example `printExact(3.00000003)` outputs `3.000000029999999`.
 */
void printExactFast(n)
    if isZero(n) then
        // Will print either 0, or null, depending on the value
        print(n);
    else
        if n < 0 then
            print("-");
            n = abs(n);
         end;
        var exp = floor(log10(n));
        var base = n * 10 ** -exp;
        var cmp = abs(base - round(base));
        if cmp <= 0 then
            // An exact integer, use native function
            print(base);
        elsif cmp > 1e-5 then
            // Mindustry Logic won't round to integer, use native function
            print(base);
        else
            // Mindustry logic would round. Use custom routine.
            print(floor(base), ".");

            // Printing the fractional part, 5 digits at a time
            // More digits would be even faster, but 5 divides the 15 digits we want to output nicely.
            for var digit in 1 .. 3 do
                base = frac(base) * 100000;
                var t = floor(base);
                if t < 10 then
                    print("0000");
                elsif t < 100 then
                    print("000");
                elsif t < 1000 then
                    print("00");
                elsif t < 10000 then
                    print("0");
                end;

                print(t);
            end;
        end;

        if exp != 0 then
            print("E", exp);
        end;
    end;
end;

/**
 * Prints the value into the text buffer without rounding to the nearest integer value.
 * The function is primarily useful for debugging purposes to determine the actual value of variables.
 * This variant of the function is optimized to produce slower, smaller code.
 *
 * **Note:** This function uses exact (slow) printing for all values except zero.
 *
 * See also [`printExactFast`](#printexactfast)
 */
void printExactSlow(n)
    if isZero(n) then
        // Will print either 0, or null, depending on the value
        print(n);
    else
        if n < 0 then
            print("-");
            n = abs(n);
         end;
        var exp = floor(log10(n));
        var base = n * 10 ** -exp;
        print(floor(base), ".");

        for var digit in 1 .. 15 do
            base = frac(base) * 10;
            print(floor(base));
        end;

        if exp != 0 then
            print("E", exp);
        end;
    end;
end;

