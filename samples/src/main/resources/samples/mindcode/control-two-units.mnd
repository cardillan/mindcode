// Binds two different units, stores them in variables and controls
// both them simultaneously by moving them around in a pattern

// Units is a system library providing functions
// for easy acquisition of units
require units;

// We'll write messages onto this block
// It is used by the units system library to output messages as well
param message = message1;

// This is a global variable
var angle = 0;

// The entire code needs to be enclosed in a code block (begin ... end)
begin
    // Finds a free unit of given type, not returning until such a unit is found
    // Sets the flags of these units to a nonzero value (1)
    // Stores the acquired units in variables
    var poly = waitForFreeUnit(message, "Control two units example\n", @poly, 1);
    var mega = waitForFreeUnit(message, "Control two units example\n", @mega, 1);

    while true do
        controlUnit(poly);
        controlUnit(mega);
    end;
end;

// A function
void controlUnit(unit)
    // Binds the unit stored in a variable
    ubind(unit);
    move(@thisx + 10 * sin(angle), @thisy + 10 * cos(angle));
    print($"Currently bound unit is ${@unit}.");
    printflush(message);
    wait(0.8);
    angle += 45;
end;
